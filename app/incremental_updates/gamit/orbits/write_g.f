      Subroutine write_g

c     Write out a g-file from the adjusted orbital parameters

      implicit none
        
      include '../includes/dimpar.h'
      include '../includes/orbits.h'
      include 'orbfit.h'
  
      integer*4 iye,idoye,ime,ide,ihe,imine,isece
     .        , jsat,ic4array,i,jj,kk

* MOD TAH 200606: Added to allow for new name designation.
      integer*4 prn, svn    ! PRN and SVN read from satnam string

      real*8 sece,adj_satics(maxorb,maxsat),glbk_adj(20)

      character*4  g_time_flag,end,glbk_icsnam(19)
      character*14 ic_time
      character*19 dattim
      character*80 head  
      
      data end/'END '/
      data glbk_icsnam/'X   ','Y   ','Z   ','XDOT','YDOT','ZDOT'
     .                ,'DRAD','YRAD','BRAD','DCOS','DSIN'
     .                ,'YCOS','YSIN','BCOS','BSIN'
     .                ,'D2CS','D2SN','D4CS','D4SN'/
        

c   Write out header information g-file
                
      call dayjul( jde,iye,idoye)
      call monday( idoye,ime,ide,iye )
      call ds2hms( iye,ide,te,ihe,imine,sece )
      isece = nint(sece)     
      write(ic_time,'(1x,i4,3i3)') iye,ime,ide,ihe
      g_time_flag = 'GPST'  
      write(lug,'(i4,1x,i3,1x,i2,1x,i2,1x,i2,18x,a4,7(1x,a5))')
     .      iye,idoye,ihe,imine,isece,g_time_flag
     .     ,frame,precmod,srpmod,nutmod,gravmod,eradmod,antradmod
      write(lug,'(i2,19(1x,a4))') nics(1),(icsnam(i),i=1,nics(1))
      head = ' '
      head(1:48) = 'G-file generated by ORBFIT'
      call runtim( dattim )
      head(49:69) = '  '//dattim
      write(lug,'(a80)') head(1:80)
      write(lug,'(a4)') end


c   Write out header information for svs file

      head(1:6) = '*  svs'
      write(lusvs,'(a80)') head(1:80)

c   Write the ICs to the g- and svs-files

      do jsat=1,nsat
        write(lug,'(a16)') satnam(jsat)   
        do i=1,nparam 
          if(islot(i).gt.100 ) then
             jj = islot(i)/100  
             kk = mod(islot(i),100)
             if( jj.eq.jsat ) then 
               adj_satics(kk,jsat) = apr_prm(i) + adjust(i) 
             endif
          endif
        enddo
        if( nics(1).eq.6 ) then
c         this is the default number of ic's for a g-file
          nics(1)= 9
          adj_satics(7,jsat)= 1.d0
          adj_satics(8,jsat)= 0.d0
          adj_satics(9,jsat)= 0.d0
        endif
        do i=1,nics(1)
          write(lug,'(d20.13)') adj_satics(i,jsat)
        enddo     
c**        write(lusvs,'(a14,a5,i2.2,2x,6f14.3,1x,9f8.4)')
c**     .     ic_time,' PRN_',isat(jsat)
c**     .    ,(1.d3*adj_satics(i,jsat),i=1,3)
c**     .    ,(1.d6*adj_satics(i,jsat),i=4,6)   
c**     .    ,(adj_satics(i,jsat),i=7,nics(1))  
c** changed by tah/rwk 041220: GLOBK expects an entry for all 14 possible rad-press 
c** parameters,even though fewer are used at one time.  
        do i=1,20       
          jj=  ic4array(glbk_icsnam(i),icsnam,maxorb)
cd          print *,'i glbk_icsnam jj ',i,glbk_icsnam(i),jj
          if( jj.gt.0 ) then
            glbk_adj(i) = adj_satics(i,jsat)
          else
            glbk_adj(i) = 0.d0
          endif 
        enddo    
* MOD TAH 200606: Outout name in new style [GREC]svn_prn.  Extract the
*       values from the satname string.
*       Format satnam -> '(a1,i2.2,1x,i4,1x,a)')
        read(satnam(jsat),'(1x,i2,1x,i4)') prn, svn
* MOD TAH 200606: Modified format for full name and more digits. 
*       Added body type (satnam(jsat)(10:))   
* NOTE: Antenna offsets need to be added if file to be used in globk.           
C       write(lusvs,'(a14,a5,i2.2,2x,6f14.3,1x,14f8.4)')
        write(lusvs,220) ic_time,satnam(jsat)(1:1),svn, prn,
     .    (1.d3*glbk_adj(i),i=1,3),
     .    (1.d6*glbk_adj(i),i=4,6),   
     .    (glbk_adj(i),i=7,20), satnam(jsat)(10:)  
 220    format(a14,1x,a1,I3.3,'_',I2.2,1x,6f16.5,1x,14f9.5,3x,a)
      enddo
c     end of loop on SVs

      write(lug,'(a4)') end
      close (lug)
      call report_stat('STATUS','ORBFIT','orbits/write_g',' '
     .                 ,'Successfully wrote G-file ',0)

      return
      end

