Copyright (c) Massachusetts Institute of Technology,1986. All rights reserved.
      SUBROUTINE LEGND2(Z,ZZ,NZONE,NTESS,LEG,LEG1,LEG2,
     $                  GLEG,GLEG1,GLEG2)
C
C        F AMUCHASTEGUI - SEPTEMBER 1969
C        RICK ABBOT OCTOBER 1984, SMALL MODIFICATIONS FOR GPS
C
C        EVALUATION OF SECOND DERIVATIVES OF LEGENDRE POLYNOMIALS AND
C        LEGENDRE FUNCTION WHICH ARE NEEDED FOR PARTIALS
C
C        LEGNDR AND LEGND2 EVALUATE THE LEGENDRE POLYNOMIALS, ITS
C        DERIVATIVES AND ALSO THE ASSOCIATED LEGENDRE FUNCTIONS
C        THE LEGENDRE POLYNOMIALS ARE DEFINED AS:
C        PN(Z) = (1 / (N|*2**N) ) * (NTH DERIVAT OF (Z*Z-1)**N)
C        AND THE ASSOCIATED LEGENDRE FUNCTIONS AS
C        P(N,H) = ((DSQRT(1-Z*Z))**H) * PH(N) ,
C        WHERE PH(N) IS THE  HTH DERIVATIVE OF  PN(Z)
C        WE CAN SEE THAT FOR H=0  P(N,0)=PN(Z)
C
C        THE FORMULAE ARE FROM  "TABLES OF INTEGRALS, SERIES
C        AND PRODUCTS" - GRADSTEYN & RYZHIK,PAGS 1004-27
CNOTE    THE FACTOR (-1)**H  FOR THE LEGENDRE FUNCTION USED IN THIS
C        REFERENCE HAS BEEN SET = 1 TO USE THE CONVENTION OF
C        THE SMITHSONIAN ASTR. OBSERVATORY JOURNAL
C        THE FORMULAE FOR  P'(Z)  AND  P''(Z)  ARE FROM "DIFFERENTIAL
C        EQUATIONS WITH APPLICATIONS", RITGER & ROSE, PAGE 223
C
      REAL*8 LEG,LEG1,GLEG,GLEG1,Z,ZZ,LEG2,GLEG2
      REAL*8 A1,A2,SIGN
      integer nzone,ntess,nsize1,i,l,k
      DIMENSION LEG(*),LEG1(*),LEG2(*),GLEG(*),GLEG1(*),GLEG2(*)
C
C        EVALUATION OF P''(Z)
      IF( NZONE.LE.1 ) GO TO 270
      LEG2(1) = 3.0D0
      IF( NZONE.LE.2 ) GO TO 270
      LEG2(2) = 15.0D0*Z
      NSIZE1 = NZONE-1
      IF( NSIZE1.LE.2 ) GO TO 270
      DO 250 I=3,NSIZE1
      LEG2(I) = LEG2(I-2) + DBLE(2*I+1)*LEG1(I-1)
 250  CONTINUE
C
C        EVALUATION OF  P''(N,H)
 270  IF( NTESS.LE.1 ) GO TO 999
      GLEG2(1) = -( 3.D0+(Z**2)/(ZZ**2) )*3.0D0*Z / ZZ
      GLEG2(2) = -6.0D0
      IF( NTESS.LE.2) GO TO 999
      L = 2
      DO 390 I=3,NTESS
      SIGN = 1.0D0
      DO 380 K=1,I
      SIGN = -SIGN
      L = L+1
      A1 = DBLE(K)
      A2 = DBLE((I-K+1)*(I+K))
      IF( K.EQ.1 ) GO TO 360
      GLEG2(L) = (A1*(GLEG(L)+Z*GLEG1(L))+2.0D0*Z*GLEG1(L)) * SIGN
     1            - (ZZ*GLEG1(L-1)-Z*GLEG(L-1)/ZZ) * A2 * SIGN
      GO TO 370
 360  GLEG2(L) =-(A1*(GLEG(L)+Z*GLEG1(L))+2.0D0*Z*GLEG1(L))
     1            + (ZZ* LEG1(I-1)-Z* LEG(I-1)/ZZ) * A2
 370  GLEG2(L) = GLEG2(L)*SIGN/(ZZ**2)
 380  CONTINUE
 390  CONTINUE
C
 999  RETURN
      END
