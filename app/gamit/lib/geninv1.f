

      SUBROUTINE PROJECT(V,N1V,NROW,NV,X,WORK)
C---------------------------------------------------------------
C        PROJECTS VECTOR X ONTO SUBSPACE SPANNED BY THE COLUMNS OF
C   THE ARRAY V BY FORMING THE MATRIX PRODUCT V*V(TRANS)X.
C  (NOTE   REMEMBER TO BE A PROJECTOR THE COLUMNS OF V MUST
C  BE ORTHONORMAL.  ONE WAY TO GET THIS IS TO USE AN SVD ROUTINE
C  TO OBTAIN THE SINGULAR VECTORS OF A MATRIX.)
C
C  ARGUMENTS-
C    V - ARRAY OF CONTAINING THE BASIS VECTORS DEFINING THE
C        SUBSPACE X IS TO BE PROJECTED ONTO.
C        THESE VECTORS ARE ASSUMED TO BE STORED IN THE COLUMNS OF V.
C    N1V-FIRST DIMENSION OF V IN CALLING PROGRAM.
C    NROW-NUMBER OF ELEMENTS IN THE ROWS OF V AND X.
C    NV-NUMBER OF VECTORS IN V.  (I.E. NUMBER OF COLUMNS IN V)
C    X-VECTOR TO BE PROJECTED.  IT IS OVERWRITTEN ON RETURN BY
C      THE REQUESTED PROJECTION.
C    WORK - WORK VECTOR OF LENGTH AT LEAST NV.
C
C  AUTHOR   GARY L. PAVLIS
C  WRITTEN   MARCH 1983
C---------------------------------------------------------------------
      INTEGER N1V,NROW,NV
      REAL V(N1V,NV),X(NROW),WORK(NV),sdot
      INTEGER J
      DO 100 J=1,NV
           WORK(J) = SDOT(NROW,V(1,J),1,X,1)
  100 CONTINUE
      DO 150 J=1,NROW
           X(J) = 0.0
  150 CONTINUE
      DO 200 J=1,NV
           CALL SAXPY(NROW,WORK(J),V(1,J),1,X,1)
  200 CONTINUE
      RETURN
      END
      SUBROUTINE COMPPRO(V,N1V,NROW,NV,X)
C---------------------------------------------------------------
C        PROJECTS VECTOR X ONTO SUBSPACE PERPENDICULAR TO
C  ORTHONORMAL BASIS VECTORS STORED IN ARRAY V. (I.E. ITS ORTHOGONAL
C  COMPLEMENT).
C
C  ARGUMENTS-
C    V - ARRAY OF CONTAINING ORTHONORMAL BASIS VECTORS DEFINING THE
C        SUBSPACE WHOSE COMPLEMENT X IS TO BE PROJECTED ONTO.
C        THESE VECTORS ARE ASSUMED TO BE STORED IN THE COLUMNS OF V.
C    N1V-FIRST DIMENSION OF V IN CALLING PROGRAM.
C    NROW-NUMBER OF ELEMENTS IN THE ROWS OF V AND X.
C    NV-NUMBER OF VECTORS IN V.  (I.E. NUMBER OF COLUMNS IN V)
C    X-VECTOR TO BE PROJECTED.  IT IS OVERWRITTEN ON RETURN BY
C      THE REQUESTED PROJECTION.
C
C  AUTHOR   GARY L. PAVLIS
C  WRITTEN   JANUARY 1983
C---------------------------------------------------------------------
      INTEGER N1V,NROW,NV
      REAL V(N1V,NV),X(NROW),sdot
      INTEGER J
      REAL SCALE
      DO 100 J=1,NV
           SCALE = SDOT(NROW,V(1,J),1,X,1)
           CALL SAXPY(NROW,-SCALE,V(1,J),1,X,1)
  100 CONTINUE
      RETURN
      END
