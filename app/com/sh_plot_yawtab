#!/bin/csh -f  
#
#doc Plot yawtab entries
#doc
#
# Script to plot yawtab yaw attitude values
# Written  by S. McClusky, Feb, 2014

if ( $#argv == 0 ) then
echo " "
echo " Use GMT to make a multiple panel GAMIT yaw attitude  plots "
echo "  "
echo "  requirements  : GMT, GAMIT"
echo "  "
echo "  USAGE :    sh_plot_yawtab   <options>. "
echo "  "
echo "  " 
echo "=+=+=+=+=+=+=+=+=+=+=+=+=+ OPTIONS +=+=+=+=+=+=+=+=+=+=+=+=+=+=+=+=+=+=+=+=+=+"   
echo " "
echo " -yt[file]          : yawtab file. MUST have this." 
echo " -yo[file]         : yawtab.out file (used to extract beta angle)"
echo "       " 
echo "       " 
echo " -prn   list       : list of satellite PRN's to plot. [Default plot all PRN's]" 
echo "       " 
echo " -old_yawtab       : plot old style yawtab " 
echo "       " 
echo " -ext ext          : file name extension " 
echo "=+=+=+=+=+=+=+=+=+=+=+=+=+=+=+=+=+=+=+=+=+=+=+=+=+=+=+=+=+=+=+=+=+=+=+=+=+=+=+"   
echo " " 
echo "  EXAMPLE:   sh_plot_yawtab  -yt yigsf3.300 -yo yawtab.out.03300 -prn 5 10 25 31" 
echo " " 
exit
else
endif
##################### SET UP DEFAULTS ############################################################
set xmin     =  0
set xmax     =  0
set ymin     =  0
set ymax     =  0
set order    =  0
set frame    =  2 
set anot     =  1 
set ta = '6H'                            # Annotations every hour
set tf = '1H'                            # Frame ticks every 15 minutesset unc_scale = 1
set unit = ( 'deg' 0.5 )                 # Coordinate units; increment for "minmax"
set prns   = ()
set file   = ""
set ADD = ""
set time_series = yes 
set delete = ""   
set delete_file = ""
set  free      =  yes
set  fixed     =  no
set  both      =  no
set  total     =  no
set  grad      = no
set  Ey        = "-Ey0.05i/1p"
set old_yawtab = "N"
set ext = ""

echo "#COLOR_MODEL = RGB" >! yaw.cpt
echo "#" >> yaw.cpt
echo "0       0 0 0         1      0 0 0       ;Nominal" >> yaw.cpt
echo "1       0 0 255       2      0 0 255     ;Night" >> yaw.cpt
echo "2       255 0 0       3      255 0 0     ;Noon" >> yaw.cpt
echo "B       yellow" >> yaw.cpt
echo "F       orange" >> yaw.cpt
echo "END" >> yaw.cpt
gmt makecpt -T-1/3/1  > ! yaw.cpt

#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#
# decipher command line
while ($#argv > 0 )
   set input = ( $argv )
   switch($input[1])
      case -yt*:
         set file     =   $input[2] 
      breaksw
      case -yo*:
         set file1     =   $input[2] 
      breaksw
      case -p*:
         set prns   =   ( $prns $input[2] ) 
         if ( $#input == 2 ) goto alldone
         shift input
         shift input 
         while ( $#input > 0 &&   `echo  $input[1]  | cut -c1 ` != "-" ) 
            set inarg =  $input[1]
            if ( `echo  $inarg | cut -c1 ` != "-" ) then
               set prns    =   ( $prns $inarg ) 
               shift input
            endif
         end
      breaksw
      case -o*:
         set old_yawtab = "Y"
      breaksw
      case -e*:
         set ext = "_${input[2]}"
      breaksw

      if ( $#input == 0 ) goto alldone
   breaksw
endsw
if ( $#argv > 0 ) shift argv
end
alldone:
#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#=#
set username = `whoami`

echo "PROCESSING gamit yawtab file: $file"

if ($#prns != 0) then
   set prns = `echo $prns | awk '{for (i=1; i<=NF; i++) printf " %02d",$i}'`
   echo "Plotting PRN's: $prns"  
endif

ytoasc $file 

set ascfile = `echo $file | awk '{print "A" substr($1,2,60)}'`

set prn_list = `grep PRN $ascfile | awk '{for (i=8; i<=NF; i++) printf " %02d",$i}'`
set blk_list = `grep BLK $ascfile | awk '{print substr($0, index($0,$3))}'`

echo "Satellite PRN's found in yawtab file: $prn_list"

if ($#prns != 0) then 
  echo "Plotting PRN's: $prns"
else
  set prns = ($prn_list)
  echo "Plotting PRN's: $prn_list"
endif

\rm -r yaw.?? >&! /dev/null
set COL = 1 
set PAGE = 1
set ROW  = 0
set psfile = yaw_$file

\rm -r $psfile.* >&! /dev/null

# scaling/title  information
gmt gmtset GMT_COMPATIBILITY 5
gmt gmtset PROJ_LENGTH_UNIT i
gmt gmtset FONT_LABEL 10p
gmt gmtset FONT_ANNOT_PRIMARY 12p

gmt gmtset FORMAT_DATE_IN yyyy-jjj
gmt gmtset FORMAT_DATE_MAP yyyy-jjj
gmt gmtset FORMAT_CLOCK_IN hh:mm:ss
gmt gmtset FORMAT_CLOCK_MAP hh:mm:ss
gmt gmtset FONT_ANNOT_SECONDARY 12p

##### loop over each PRN and plot#########

foreach p ($prns)
   set i = 0
   foreach q ($prn_list)
      @ i = $i + 1
      if ($q == $p ) then
        tail -n +8 $ascfile | awk -v i=$i '{ if ( $(6+i*2) <= 360.0 ) {printf("%04d-%03dT%02d:%02d:%02d %7.2f %d\n",$3,$4,$5,$6,$7,$(6+i*2),$(7+i*2))} else {printf("%04d-%03dT%02d:%02d:%02d %7.2f %d\n",$3,$4,$5,$6,$7,$(6+i*2)-360.0,$(7+i*2))}}'>! yaw.${prn_list[${i}]}
        break
      endif
   end

# Set range and axis info
   set RANGE = `gmt gmtinfo -f0T,1f -I10m/5 yaw.${p} --TIME_UNIT=m`
   set r = ( `echo $RANGE | awk -v ta=$ta -v tf=$tf -F'/' 'BEGIN {n=split("0.1 0.2 0.5 1 2 5 10 20 50 100 360 5e2 1e3 2e3 5e3",a," ")}; {for (i=1; i<=n; i++) if (int(($4-$3)/a[i]) < 10) print "-Bpxa"ta"f"tf" -Bya"a[i]"f"a[i]/2}'` )   

   @ ROW = $ROW + 1 
   if ( $ROW == 5 ) then 
      set ROW = 1 
      @ PAGE = $PAGE + 1 
   endif

# Get hearder info
   if ( $file1 != "" ) then
      set prn    =  `cat $file1 | grep "PRN G0${prn_list[${i}]}" | awk '{print substr($2,3,2)}'`
      set svn    =  `cat $file1 | grep "PRN G0${prn_list[${i}]}" | awk '{print substr($4,3,2)}'`
      set blk_n  =  `cat $file1 | grep "PRN G0${prn_list[${i}]}" | awk '{print substr($6,3,2)}'`
      set blk    =  `cat $file1 | grep "PRN G0${prn_list[${i}]}" | awk '{print $7}'`
      set maxyaw =  `cat $file1 | grep "PRN G0${prn_list[${i}]}" | awk '{print $8}'`
      set ybias  =  `cat $file1 | grep "PRN G0${prn_list[${i}]}" | awk '{print $9}'`
      set beta   =  `cat $file1 | grep "PRN G0${prn_list[${i}]}" | awk '{print $10}'`
     echo ${prn_list[${i}]} ${blk_list[${i}]} | awk -v prn=$prn -v svn=$svn -v blkn=$blk_n -v blk=$blk -v my=$maxyaw -v yb=$ybias -v beta=$beta  '{printf"-4.0 1.07  PRN: %02d  SVN: %02d BLOCK: %01d ( %s ) MAX YAW RATE: %f Y-BIAS: %5.1f  BETA ANGLE: %f \n",$1,svn,$2,blk,my,yb,beta}'  >! tmp.txt
   else
     echo ${prn_list[${i}]} ${blk_list[${i}]} | awk '{printf"-4.0 1.07  PRN: %02d  BLOCK: %01d \n",$1,$2}'  >! tmp.txt
   endif

   if ( $ROW == 1 ) then 
      gmt pstext tmp.txt   -R-4/1/0/1 -JX6.5i/1.8i -F+f9p+jML -K -P -N    -Y8.5i    >!   ${psfile}${ext}.${PAGE}.ps	
   else
      gmt pstext tmp.txt   -R-4/1/0/1 -JX6.5i/1.8i -F+f9p+jML -K -P -O -N -Y-2.5i   >>   ${psfile}${ext}.${PAGE}.ps	
   endif

   if ( $ROW != 4   ) then 
      gmt psxy yaw.${p} $RANGE -Sc0.05i -Cyaw.cpt -JX6.5i/1.8i   -G0 -K -O  ${r[2]}+l'Yaw Angle' -Bsxa1D -BWeSn -P   >>   ${psfile}${ext}.${PAGE}.ps
   else
      gmt psxy yaw.${p} $RANGE -Sc0.05i -Cyaw.cpt -JX6.5i/1.8i   -G0 -K -O  ${r[2]}+l'Yaw Angle' -Bsxa1D -BWeSn -P   >>   ${psfile}${ext}.${PAGE}.ps
      echo "-4.0 0.9  PAGE" $PAGE >! tmp.page
      gmt pstext tmp.page   -R-4/1/0/1 -JX6.5i/1.8i -F+f9p+jML -P -O -N -Y-2.5i   >>   ${psfile}${ext}.${PAGE}.ps	
   endif

echo "Created"  $psfile${ext}.$PAGE.$ROW

# End while - do next PRN
end 

echo ".-4 0.9  PAGE" $PAGE >! tmp.page
gmt pstext tmp.page   -R0/1/0/1 -JX6.5i/1.8i -F+f9p+jML -P -O -N -Y-2.5i   >>   $psfile${ext}.$PAGE.ps	

cleanup: 
echo "  "
if ( $PAGE > 1 ) then
echo "Created  postript files    : "$psfile${ext}".1-"$PAGE.ps
else 
echo "  "
echo "Created  postript files    : "$psfile${ext}".1.ps" 
endif
echo "  "
echo "To view  postript files    : gs "$psfile${ext}".*.ps"
echo "  "

\rm -rf  tmp.0  tmp.txt tmp.page yaw.??

exit

