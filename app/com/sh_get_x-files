#!/bin/csh
#
# Script to x-files for GIPSY analyses.  UNR values are coded
# first

# ftp://sideshow.jpl.nasa.gov/pub/JPL_GPS_Products/Final/2018/2018-01-01_nnr.x.gz

if( $#argv == 0 ) then
   echo "Uasage: sh_get_x-files <archive> <center> <frame> <directory>"
   echo 'where <archive> is file to save scale results'
   echo '      <center>  is UNR/JPL'
   echo '      <frame>   is frame name  NA12 for UNR, nnr for JPL'
   echo '      <directory> for x-files'
   exit
endif

# Set defaults
set dir = UNR_x-files
set frame = NA12
set cen = UNR

if( $#argv >= 1 ) then
    set arc = $1
endif
if( $#argv >= 2 ) then
    set cen = $2
    if ( $cen == 'JPL' ) then
       set frame = "nnr"
       set dir = "JPL_x-files"
    endif
endif

if( $#argv >= 3 ) then
    set frame = $3
endif

if( $#argv == 4 ) then
    set dir = $4
endif

echo "Creating/Updating $arc with $cen $frame x-files in $dir"

set months = `echo "JAN FEB MAR APR MAY JUN JUL AUG SEP OCT NOV DEC"`

# Add other options
if ( ! -e $dir ) mkdir $dir

cd $dir
# See if archive exits.  If not create now
if( ! -e ../$arc ) then
    echo "Creating $arc from scratch"
    echo "# X-file scale created for center $cen, frame $frame" >! ../$arc
    echo "# Created `date`" >> ../$arc
    echo "# x-files stored in $dir" >> ../$arc
    echo "#   MJD          SCALE          SCALE +-" >> ../$arc
#
    foreach yr (`seq 1996 2017`)
      echo $yr
      foreach im ( `seq 1 12` )
         echo $im $months[$im]
         foreach d (`seq 1 31`)
           if( $cen == "UNR" ) then
              set file = `echo $yr $months[$im] $d $frame | awk '{printf("%2.2d%s%2.2d.%s.x",substr($1,3,2),$2,$3,$4)}'`
              if( ! -e $file ) then
                  echo "wget  $file"
                  set OK  = `wget  ftp://gneiss.nbmg.unr.edu/x-files/${file} |& grep "No such file"`
              else
                  echo "Exits $file"
                  set OK = ''
              endif
           else if ( $cen == 'JPL') then
#2018-01-01_nnr.x.gz
              set file = `echo $yr $im $d $frame | awk '{printf("%4d-%2.2d-%2.2d_%s.x",$1,$2,$3,$4)}'`
              if( ! -e $file ) then
                  echo "wget  $file"
                  set OK  = `wget  ftp://sideshow.jpl.nasa.gov/pub/JPL_GPS_Products/Final/${yr}/${file}.gz |& grep "No such file"`
                  gzip -d ${file}.gz
              else
                  echo "Exits $file"
                  set OK = ''
              endif
           endif

           if ( $#OK <= 1 ) then 
#             file downloaed OK
              set mjd = `doy  $yr $im $d 12 00 | head -1 | awk '{print $10}'`
              set scale = `grep "^ S  = " $file | awk '{printf(" %10.4f %10.4f \n",$3*1.e9,$5*1.e9)}'`
              echo $mjd $scale | awk '{printf(" %8.2f  %10.4fE-09 %10.4fE-09 \n",$1,$2,$3)}' >> ../${arc}
           endif
         end
       end
    end
else
#   Append to current file.  Get the end of the current file
    set endmjd = `grep '^ ' ../$arc | tail -n -1 | awk '{printf(" %d", $1+0.5)}'`
    set curmjd = `doy | tail -3 | head -1 | awk '{printf(" %d", int($10))}'`
    # Add comment
    echo "Updating files from $endmjd to $curmjd"
    echo "# Updated to $curmjd" >> ../${arc}
    foreach mjd ( `seq $endmjd $curmjd` )
       set ymd = `doy $mjd | head -1 | awk '{printf(" %4d %2.2d %2.2d",substr($2,1,4),substr($2,6,2),substr($2,9,2))}'`

       if( $cen == "UNR" ) then
          set file = `echo $ymd[1] $months[$ymd[2]] $ymd[3] $frame | awk '{printf("%2.2d%s%2.2d.%s.x",substr($1,3,2),$2,$3,$4)}'`
          if( ! -e $file ) then
              echo "wget  $file"
              set OK  = `wget  ftp://gneiss.nbmg.unr.edu/x-files/${file} |& grep "No such file"`
          else
              echo "Exits $file"
              set OK = ''
          endif
       else if ( $cen == "JPL" ) then
          set file = `echo $ymd $frame | awk '{printf("%4d-%2.2d-%2.2d_%s.x",$1,$2,$3,$4)}'`
           if( ! -e $file ) then
               echo "wget  $file"
               set OK  = `wget  ftp://sideshow.jpl.nasa.gov/pub/JPL_GPS_Products/Final/${ymd[1]}/${file}.gz |& grep "No such file"`
               gzip -d ${file}.gz
           else
               echo "Exits $file"
               set OK = ''
           endif
       endif 

       if ( $#OK <= 1 ) then 
#         file downloaed OK
          set mjd = `doy  $ymd 12 00 | head -1 | awk '{print $10}'`
          set scale = `grep "^ S  = " $file | awk '{printf(" %10.4f %10.4f \n",$3*1.e9,$5*1.e9)}'`
          echo $mjd $scale | awk '{printf(" %8.2f  %10.4fE-09 %10.4fE-09 \n",$1,$2,$3)}' >> ../${arc}
       endif
    end 
#      Possiblie code for JPL Side show 
endif

               


