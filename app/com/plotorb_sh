#!/bin/csh -f
#
#doc Plot orbit difference or residual files generated by ORBDIF and ORBFIT
#doc
#
# By Peng Fang , SIO, UCSD, Dec. 3, 92
# Modified by Simon Mcclusky Nov. 22 93, June 30 94, May 03 95.
#
echo "   "
echo " PLOTORB is a script for plotting files generated by ORBDIF and ORBFIT " 
echo "   "
#
#----------------------------------------------------------
#  prepare headers
#----------------------------------------------------------
set ext = $1
if($2 != '')then
  set yn = $2      
else
  set yn = n
endif

if ( $ext == '' ) then
  echo -n " enter file extension "
  set ext = $<   
  set file_ext = '_'$ext 
else
  set file_ext = '_'$ext
endif
echo 'Output files will be prn??'$file_ext'.*'

foreach pltf (plt$file_ext.??)   

if ($yn == "all" ) goto skip 

echo -n " plot" $pltf "? (y/n/all/Ctrl-C=quit) "
set yn = $<
if ($yn == "n" || $yn == "N") goto next  
 
skip:
#----------------------------------------------------------
#  prepare the plot file
#----------------------------------------------------------

set num = `echo $pltf | awk '{FS="."} {print substr($2,1,2)}'`

awk 'NR > 5 {print $1,$2}' $pltf >! tmpx
awk 'NR > 5 {print $1,$3}' $pltf >! tmpy
awk 'NR > 5 {print $1,$4}' $pltf >! tmpz

set ylab1 = `head -n 1 $pltf`
set ylabt = `head -n 1 $pltf | awk ' {print $1}'`
set ylab2 = `head -n 2 $pltf | tail -n -1`
set ylab3 = `head -n 3 $pltf | tail -n -1`
set xlab = `head -n 4 $pltf | tail -n -1`
set xmax = `head -n 5 $pltf | tail -n -1`
set ymax1 = `head -n 6 $pltf | tail -n -1`
set ymax2 = `head -n 7 $pltf | tail -n -1`
set ymax3 = `head -n 8 $pltf | tail -n -1`

set file1 = tmpx
set file2 = tmpy
set file3 = tmpz
set testt = "V-X "

#echo  "testt $testt  "
#echo  "ylabt $ylabt  "

if ( $ylabt == $testt ) then
pstext -Jx1 -R0/1/0/1 -K -N << eof >! prn$num$file_ext.ps 
4.7 6.8 20 0.0 1 6 Instantaneous Orbit Velocities for Satellite PRN $num
0 5.5 16 90.0 1 6 $ylab1
0 3.4 16 90.0 1 6 $ylab2
0 1.3 16 90.0 1 6 $ylab3
4.7 -.2 14 0.0 1 6 $xlab   
1 -0.5 12 0.0 1 6 $ext
eof 

else 
pstext -Jx1 -R0/1/0/1 -K -N << eof >! prn$num$file_ext.ps 
4.7 6.8 20 0.0 1 6  Orbit Positional Differences for Satellite PRN $num
0 5.5 16 90.0 1 6 $ylab1
0 3.4 16 90.0 1 6 $ylab2
0 1.3 16 90.0 1 6 $ylab3
4.7 -.2 14 0.0 1 6 $xlab
0 -0.5 12 0.0 1 6 $ext
eof

endif 
echo " plotting prn$num$file_ext.ps "                                                                                                    

#----------------------------------------------------------
#  set extremes
#----------------------------------------------------------
#echo " ymax1 $ymax1 ymax2 $ymax2 ymax3 $ymax3 "
set xmin = 0
#@ xmax = $xmax + 1  

set ymax1 = `echo $ymax1 | awk '{print ($1 + 0.1)}'` 
set ymax2 = `echo $ymax2 | awk '{print ($1 + 0.1)}'` 
set ymax3 = `echo $ymax3 | awk '{print ($1 + 0.1)}'`     

#echo " ymax1 $ymax1 ymax2 $ymax2 ymax3 $ymax3 "
 
set ymin1 = -$ymax1
set ymax1 = $ymax1 
set ymin2 = -$ymax2
set ymax2 = $ymax2
set ymin3 = -$ymax3
set ymax3 = $ymax3


set xlength = 8.5
set ylength = 1.8


set scal1 = `echo $xmin $xmax $xlength $ymin1 $ymax1 $ylength`
set scal1 = `echo $scal1 | awk '{xscl = $3 / ( $2 - $1); yscl = $6 / ( $5 - $4 ); print "-Jx"xscl"/"yscl}'`

set scal2 = `echo $xmin $xmax $xlength $ymin2 $ymax2 $ylength`
set scal2 = `echo $scal2 | awk '{xscl = $3 / ( $2 - $1); yscl = $6 / ( $5 - $4 ); print "-Jx"xscl"/"yscl}'`

set scal3 = `echo $xmin $xmax $xlength $ymin3 $ymax3 $ylength`
set scal3 = `echo $scal3 | awk '{xscl = $3 / ( $2 - $1); yscl = $6 / ( $5 - $4 ); print "-Jx"xscl"/"yscl}'`


set limi1 = '-R'$xmin/$xmax/$ymin1/$ymax1
set limi2 = '-R'$xmin/$xmax/$ymin2/$ymax2
set limi3 = '-R'$xmin/$xmax/$ymin3/$ymax3

#echo -n "xmax $xmax"
#set ent = $<

	     set xspread = `echo  $xmax  $xmin | awk '{ printf "%6d",  $1 - $2}'`
	     if ( $xspread < 50 )  set xanot  = 2
	     if ( $xspread > 70 )  set xanot  = 10
	     if ( $xspread > 100 ) set xanot  = 10
	     if ( $xspread > 200)  set xanot  = 10
	     if ( $xspread > 300)  set xanot  = 20
	     if ( $xspread > 500)  set xanot  = 40
	     set  xframe = ` echo $xanot | awk '{print $1 / 2 }'`     

	     set yspread = `echo  $ymax1  $ymin1 | awk '{ printf "%8.2f",  $1*100 - $2*100}'` 
        set yanot1 = `echo $yspread | awk '{ printf "%8.2f", ($1/5)/100}'`
	     set  yframe1 = ` echo $yanot1 | awk '{print $1 / 2 }'`

	     set yspread = `echo  $ymax2  $ymin2 | awk '{ printf "%8.2f",  $1*100 - $2*100}'`
        set yanot2 = `echo $yspread | awk '{ printf "%8.2f", ($1/5)/100}'`
	     set  yframe2 = ` echo $yanot2 | awk '{print $1 / 2 }'`

	     set yspread = `echo  $ymax3  $ymin3 | awk '{ printf "%8.2f",  $1*100 - $2*100}'`
        set yanot3 = `echo $yspread | awk '{ printf "%8.2f", ($1/5)/100}'`
	     set  yframe3 = ` echo $yanot3 | awk '{print $1 / 2 }'`

        set framn = "-Ba"$xanot"f"$xframe"/a"$yanot1"f"$yframe1"::Wesn"
        set framo = "-Ba"$xanot"f"$xframe"/a"$yanot2"f"$yframe2"::Wesn"   
        set frame = "-Ba"$xanot"f"$xframe"/a"$yanot3"f"$yframe3"::WeSn"

#----------------------------------------------------------
#  prepare headers
#----------------------------------------------------------
tail -n +4 $file1 >! $file1.tmp
tail -n +4 $file2 >! $file2.tmp
tail -n +4 $file3 >! $file3.tmp
psxy $file1.tmp $framn $limi1 $scal1 -Sb.01 -X0.7 -Y4.60  -O -K >> prn$num$file_ext.ps
psxy $file2.tmp $framo $limi2 $scal2 -Sb.01       -Y-2.1  -O -K >> prn$num$file_ext.ps
psxy $file3.tmp $frame $limi3 $scal3 -Sb.01       -Y-2.1  -O    >> prn$num$file_ext.ps

#----------------------------------------------------------
#  view the plot
#----------------------------------------------------------

# gs prn$num.ps &

next: 

end   

#----------------------------------------------------------
# view and print insrtuctions
#----------------------------------------------------------
echo "  "
echo " To view orbit comparison plots on screen use: " 
echo "  "
echo " gs prn*"$file_ext".ps "
echo "  "
echo " To send orbit comparison plots to printer use: " 
echo "  "
echo " lpr -s prn*"$file_ext".ps "
echo "  "


