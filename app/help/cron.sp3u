#!/bin/csh -f
# 6-hourly cron to download IGS ultra-rapid orbits from cddis
# The cron should run UTC 3:01, 9:01, 15:01, and 21:01
# Cron below is for Boston daylight savings time (our crons
#   run on local time).  The script test for UTC time and 
#   delays as needed).
# 01 4-23/6 * * * csh /home/tah/cron.sp3u > /dev/null 2>&1
#
set sp3_dir = /net/chandler/raid8/tah/RealTime/sp3_files
#
cd $sp3_dir >&! /dev/null
if( ! -e cron_sp3u.log ) then
  echo `date` "Creating sp3 ultrapid download log" >! cron_sp3u.log
  echo "--------------------------------------------------------------" >> cron_sp3u.log
endif
#
# Get current UTC time 
set time = `date -u +"%Y %m %d %H %M"`
# Generate the name of sp3 file needed and see if we should
# wait for it to arrive (i.e., EST versus EDT).
set wait = `echo $time[4] | awk '{print int(($1-3)/6)*6-$1+3}' | awk '{if( $1 < 0 ) {print $1+6} else {print $1}}'`
#
# Generate the name of SP3 file needed
set wd = `doy $time[1] $time[2] $time[3]| head -2 | tail -n -1 | awk '{printf("%4d %d",$3,$7)}'`
# Normally $wait should 0 during EST and 1hr when EDT in effect (line below can have problems if delay is too long)
set sp3f = `echo $wd $time[4] $wait | awk '{if($3+$4-3 < 24 ) {printf("igu%4.4d%1d_%2.2d.sp3",$1,$2,$3+$4-3)} else {printf("igu%4.4d%1d_%2.2d.sp3.Z",$1,$2,$3+$4-3-24)}}'`
echo STATUS: `date +"%Y%m%d-%H%M"` : Getting $sp3f wait $wait hrs >> cron_sp3u.log

# See if we need to sleep
if( $wait > 0 ) then
  sleep ${wait}h
endif
# Try to download file from cddis
set pw = `whoami`@`hostname`
while ( ! -e  $sp3f )
    ftp -ivn cddis.gsfc.nasa.gov <<! >&! /dev/null
user anonymous $pw
cd pub/gps/products/$wd[1]
ls $sp3f.Z
get $sp3f.Z
!
#   Try to uncompress
    if( -e  ${sp3f}.Z ) then
       uncompress ${sp3f}.Z
    else
       echo STATUS: `date +"%Y%m%d-%H%M"` : Getting $sp3f wait 1 minute >> cron_sp3u.log
       sleep 1m
    endif
end
#
# Thats all




